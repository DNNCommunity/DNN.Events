/************************************************************/
/*****                                                  *****/
/*****       SqlDataProvider                            *****/
/*****       Install/Upgrade for Events module 06.02.03 *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search AND replace operation     *****/
/*****       for {databaseOwner} AND {objectQualifier}  *****/
/*****       or use 'SQL' FROM Host Menu AND run this.  *****/
/*****                                                  *****/
/************************************************************/

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}EventsModerateSignups]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}EventsModerateSignups]
GO

/*** EventsModerateSignups ***/
CREATE PROCEDURE {databaseOwner}[{objectQualifier}EventsModerateSignups]
(
    @ModuleID int,
    @SocialGroupId int
)
AS
BEGIN
SET NOCOUNT ON;
SET DATEFORMAT mdy;

SELECT	s.EventID,
		s.SignupID,
		s.ModuleID,
		s.Userid,
		s.Approved,
		u.DisplayName as UserName,
		u.Email,
		c.EventDateBegin,
		c.EventTimeBegin,
		c.EventName,
		c.Importance,
		c.Approved as EventApproved,
		c.MaxEnrollment,
		(	SELECT COUNT(*)
			FROM {databaseOwner}[{objectQualifier}EventsSignups]
			WHERE EventID = c.EventID AND c.Signups = 1
		) as Enrolled,
        PayPalStatus, 
        PayPalReason, 
        PayPalTransID, 
        PayPalPayerID, 
        PayPalPayerStatus, 
        PayPalRecieverEmail, 
        PayPalUserEmail,
        PayPalPayerEmail, 
        PayPalFirstName, 
        PayPalLastName, 
        PayPalAddress, 
        PayPalCity, 
        PayPalState, 
        PayPalZip, 
        PayPalCountry, 
        PayPalCurrency, 
        PayPalPaymentDate, 
        PayPalAmount, 
        PayPalFee,
        AnonEmail,
        AnonName,
        AnonTelephone,
        AnonCulture,
        AnonTimeZoneId,
		S.FirstName,
		S.LastName,
		S.Company,
		S.JobTitle,
		S.ReferenceNumber,
		S.Street,
		S.PostalCode,
		S.City,
		S.Region,
		S.Country
FROM {databaseOwner}[{objectQualifier}EventsRecurMaster] AS r RIGHT OUTER JOIN
     {databaseOwner}[{objectQualifier}Events] AS c ON r.RecurMasterID = c.RecurMasterID RIGHT OUTER JOIN
     {databaseOwner}[{objectQualifier}EventsSignups] AS s LEFT OUTER JOIN
     {databaseOwner}[{objectQualifier}Users] AS u ON s.UserID = u.UserID ON c.EventID = s.EventID
Where s.Approved = 0
  AND s.ModuleID = @ModuleID
  AND (r.SocialGroupId = @SocialGroupId or (r.SocialGroupId is null and @SocialGroupId = -1))
ORDER BY c.EventDateBegin, c.EventTimeBegin, c.EventName, UserName
END
GO

/*** EventsGetByRange ***/
if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}EventsGetByRange]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}EventsGetByRange]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}EventsGetByRange]
(
 @ModuleIDs nvarchar(1024),
 @BeginDate datetime,
 @EndDate datetime,
 @CategoryIDs nvarchar(1024),
 @LocationIDs nvarchar(1024),
 @SocialGroupId int,
 @SocialUserId int
)
AS
BEGIN
SET NOCOUNT ON;
SET DATEFORMAT mdy;

CREATE Table #EventIDs
    (EventID Int, NoOfRecurrences Int, LastRecurrence DateTime)

INSERT INTO #EventIDs (EventID, NoOfRecurrences, LastRecurrence) 
SELECT DISTINCT e.EventID, Count(E2.EventID) as NoOfRecurrences, Max(E2.EventTimeBegin) as LastRecurrence
 FROM {databaseOwner}[{objectQualifier}Events] E
 LEFT OUTER JOIN {databaseOwner}[{objectQualifier}EventsMaster] M ON E.ModuleID = M.SubEventID 
 LEFT JOIN {databaseOwner}[{objectQualifier}Events] as E2 ON E.RecurMasterID = E2.RecurMasterID
 WHERE ((E.EventTimeBegin < DATEADD(DAY,1,@EndDate) AND DATEADD(minute,E.Duration,E.EventTimeBegin) >=  @BeginDate) OR 
   (E.EventTimeBegin >= @BeginDate AND E.EventTimeBegin < DATEADD(DAY,1, @EndDate))) 
   AND E.Approved = 1
   AND E.Cancelled = 0
   AND (E.ModuleID in (SELECT * FROM {databaseOwner}[{objectQualifier}EventsSplitIDs](@ModuleIDs)))
GROUP By E.EventID

SELECT E.PortalID, E.EventID, E.RecurMasterID, E.ModuleID, E.EventDateBegin, E.EventDateEnd,
     E.EventTimeBegin, E.Duration, E.EventName, E.EventDesc,
     E.Importance, E.CreatedDate, 
     CreatedBy = U.DisplayName, 
     E.CreatedByID, 
     E.Every, 
     E.Period, 
     E.RepeatType, 
     E.Notify, 
     E.approved, 
     E.Signups, 
     E.MaxEnrollment, 
     (Select Sum(NoEnrolees) from {databaseOwner}[{objectQualifier}EventsSignups] WHERE EventID = E.EventID and E.Signups = 1) as Enrolled, 
     ET.NoOfRecurrences,
     ET.LastRecurrence,
     E.EnrollRoleID, 
     E.EnrollFee, 
     E.EnrollType,
     E.PayPalAccount, 
     E.Cancelled, 
     E.DetailPage, 
     E.DetailNewWin, 
     E.DetailURL, 
     E.ImageURL, 
     E.ImageType, 
     E.ImageWidth, 
     E.ImageHeight, 
     E.ImageDisplay, 
     E.Location, 
     c.LocationName, 
     c.MapURL, 
     E.Category, 
     b.CategoryName, 
     b.Color, 
     b.FontColor, 
     E.Reminder, 
     E.SendReminder, 
     E.ReminderTime, 
     E.ReminderTimeMeasurement, 
     E.ReminderFrom, 
     E.SearchSubmitted, 
     E.CustomField1,
     E.CustomField2, 
     E.EnrollListView, 
     E.DisplayEndDate, 
     E.AllDayEvent, 
     E.OwnerID, 
     OwnerName = O.DisplayName, 
     E.LastUpdatedAt, 
     LastUpdatedBy = L.DisplayName, 
     E.LastUpdatedID, 
     RMOwnerID = r.OwnerID, 
     r.RRULE, 
     E.OriginalDateBegin, 
     E.NewEventEmailSent,
     r.EventTimeZoneId,
     E.AllowAnonEnroll,
     E.ContentItemId,
     E.JournalItem,
     r.SocialGroupId,
     r.SocialUserId,
     E.Summary,
     E.Sequence,
     RMSequence = r.Sequence,
     SocialUserUserName = S.UserName,
     SocialUserDisplayName = S.DisplayName
 FROM {databaseOwner}[{objectQualifier}Events] E 
 inner join {databaseOwner}[{objectQualifier}EventsRecurMaster] AS r on E.RecurMasterID = r.RecurMasterID 
 left outer join {databaseOwner}[{objectQualifier}Users] U on E.CreatedByID = U.UserID 
 left outer join {databaseOwner}[{objectQualifier}Users] O on E.OwnerID = O.UserID 
 left outer join {databaseOwner}[{objectQualifier}Users] L on E.LastUpdatedID = L.UserID 
 left outer join {databaseOwner}[{objectQualifier}Users] S on r.SocialUserId = S.UserID 
 left join {databaseOwner}[{objectQualifier}EventsCategory] b on E.Category = b.Category 
 left join {databaseOwner}[{objectQualifier}EventsLocation] c on E.Location = c.Location 
 left join #EventIDs ET on E.EventID = ET.EventID
WHERE E.EventID in (Select EventID from #EventIDs)
   AND (e.Category in (SELECT * FROM {databaseOwner}[{objectQualifier}EventsSplitIDs](@CategoryIDs)) or @CategoryIDs = '-1')
   AND (e.Location in (SELECT * FROM {databaseOwner}[{objectQualifier}EventsSplitIDs](@LocationIDs)) or @LocationIDs = '-1')
   AND (r.SocialGroupId = @SocialGroupId or @SocialGroupId = -1)
   AND (r.SocialUserId = @SocialUserId or @SocialUserId = -1)
ORDER BY e.EventTimeBegin
END

GO
